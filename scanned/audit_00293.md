# Miscalculation of cancel amount when canceling a vesting plan

## Summary
No summary provided.

## Details
##### Description

Upon cancellation of a vesting plan, the claimed asset containing the claimable amount is returned, along with the cancel amount designed to cover the remaining vesting period. This cancel amount is derived by subtracting the claimed amount from the total amount allocated within the vesting plan. The claimed amount is determined prior to the subtraction, being incremented with the output of `get_claimable`, which is computed through the following process:

```
public fun get_claimable(_vesting_plan: &VestingPlan): u64 {
    if (_vesting_plan.paused) {
        return 0
    };
    let now = timestamp::now_seconds();
    _vesting_plan.initial_amount + safe_mul_div(
        _vesting_plan.total_amount,
        min(now, _vesting_plan.end_at_sec) - min(now, _vesting_plan.start_at_sec),
        (_vesting_plan.end_at_sec - _vesting_plan.start_at_sec)
    ) - _vesting_plan.claimed_amount
}
```

This illustrates that the claimed amount will fluctuate within the range from `(_vesting_plan.initial_amount - _vesting_plan.claimed_amount)` to `(_vesting_plan.initial_amount + _vesting_plan.total_amount - _vesting_plan.claimed_amount)`, wherein the claimed amount is initially set to zero upon the creation of the vesting plan.

Subsequently, it is incremented by the output of `get_claimable` during either a `claim` or `cancel` operation when calling `claim_internal`. Consequently, if`_vesting_plan.initial_amount`is greater than zero, the cancel amount will consistently be incorrect, holding an amount that is lesser by `_vesting_plan.initial_amount`, potentially leading to underflow issues because `_vesting_plan.claimed_amount` can hold up to `_vesting_plan.total_amount + _vesting_plan.initial_amount`.

Consequently, two distinct scenarios emerge:

* Certain users may find themselves unable to cancel their vesting plans, thereby being compelled to continue with the vesting schedule against their intentions.
* External modules relying on the cancel amount to refund users their vested tokens may inadvertently refund less than the requisite amount, potentially result in financial loss for the affected users.

Code Location
-------------

The `cancel` function does not incorporate the vesting plan's initial amount when calculating the cancel amount:

```
public fun cancel(_vesting_plan: VestingPlan, _claim_cap: ClaimCapability, _admin_cap: AdminCapability): (FungibleAsset, u64) {
    assert!(_vesting_plan.paused == false, E_PAUSED);
    assert!(_vesting_plan.uid == _claim_cap.uid && _vesting_plan.uid == _admin_cap.uid, E_NOT_AUTHORIZED);
    let claimable = get_claimable(&_vesting_plan);
    let claimed_asset = fungible_asset::zero(claimable_fa_store::get_metadata_by_uid(&_vesting_plan.claimable_fa_store_claim_cap));
    if (claimable > 0) {
        fungible_asset::merge(&mut claimed_asset, claim_internal(&mut _vesting_plan, claimable));
    };
    let cancel_amount = _vesting_plan.total_amount - _vesting_plan.claimed_amount;

    (claimed_asset, cancel_amount)
}
```

The `claim_internal` function increments the vesting plan's `claimed_amount` with the provided amount:

```
fun claim_internal(_vesting_plan: &mut VestingPlan, _amount: u64): FungibleAsset {
    _vesting_plan.claimed_amount = _vesting_plan.claimed_amount + _amount;
    claimable_fa_store::claim_funding_store(&_vesting_plan.claimable_fa_store_claim_cap, _amount)
}
```

##### BVSS

[AO:A/AC:L/AX:L/R:P/S:U/C:N/A:M/I:H/D:N/Y:N (4.4)](/bvss?q=AO:A/AC:L/AX:L/R:P/S:U/C:N/A:M/I:H/D:N/Y:N)

##### Recommendation

Consider changing the `cancel_amount` calculation as follows:

```
let cancel_amount = _vesting_plan.initial_amount + _vesting_plan.total_amount - _vesting_plan.claimed_amount;
```

Remediation Plan
----------------

**SOLVED:** The **Taurus Labs team** solved the issue by deducting the initial amount from the total amount within the `get_claimable` function:

```
_vesting_plan.initial_amount + safe_mul_div(
    _vesting_plan.total_amount - _vesting_plan.initial_amount,
    min(now, _vesting_plan.end_at_sec) - min(now, _vesting_plan.start_at_sec),
    (_vesting_plan.end_at_sec - _vesting_plan.start_at_sec)
) - _vesting_plan.claimed_amount
```

##### Remediation Hash

<https://github.com/tauruslabs/merkle-contract/commit/e368a744c4e03e9bfd7ac517a96e1f746d9474e1>

##### References

[tauruslabs/merkle-contract/merkle-token/sources/vesting.move#L126](https://github.com/tauruslabs/merkle-contract/blob/8b9d5bb6a65adfd62654daaba4061287714b2594/merkle-token/sources/vesting.move#L126)
