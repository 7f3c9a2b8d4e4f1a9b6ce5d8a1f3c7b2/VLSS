# [M-03] The proposal expiration logic is incorrect

## Summary
The `is_proposal_expired` function has a bug where it uses the wrong comparison logic, causing proposals to be marked as expired when they should still be active, and vice versa. This is because of a reversed comparison operator in the expiration check. This bug has a high impact as it prevents legitimate voting and effectively reverses the voting period enforcement. This can lead to a denial of service (DoS) as any multisig wallet created would be unable to execute proposals. The bug is present in `multisig.move` files in both `initia_stdlib` and `minitia_stdlib`. A proof of concept has been provided to demonstrate the bug and a recommended fix has been suggested. The severity of the bug was initially disputed, but it was eventually classified as a medium severity issue by the judge.

## Details
The `is_proposal_expired` function uses incorrect comparison logic that causes proposals to be marked as expired when they should still be active, and vice versa. This is as a result of the reversed comparison operator in the expiration check.

The impact of this bug is high because valid proposals are incorrectly marked as expired which prevents legitimate voting. Also the voting period enforcement is effectively reversed. This effectively creates a DoS because any multisig wallet created would be unable to execute proposals.

> N/B: This issue is present in `multisig.move` files in both `initia_stdlib` and `minitia_stdlib`

### Proof of Concept

In the `is_proposal_expired` function:

<https://github.com/initia-labs/movevm/blob/7096b76ba9705d4d932808e9c80b72101eafc0a8/precompile/modules/minitia_stdlib/sources/multisig.move# L483-L500>
```

File: multisig.move
483:     fun is_proposal_expired(
484:         max_period: &Period, proposal_height: u64, proposal_timestamp: u64
485:     ): bool {
486:         let (height, timestamp) = get_block_info();
487:         let expired_height =
488:             if (option::is_some(&max_period.height)) {
489:                 let max_voting_period_height = *option::borrow(&max_period.height);
490:  >>>>           (max_voting_period_height + proposal_height) >= height
491:             } else { false };
492:
493:         let expired_timestamp =
494:             if (option::is_some(&max_period.timestamp)) {
495:                 let max_voting_period_timestamp = *option::borrow(&max_period.timestamp);
496: >>>>            (max_voting_period_timestamp + proposal_timestamp) >= timestamp
497:             } else { false };
498:
499:         expired_height || expired_timestamp
500:     }
```

Consider a scenario:

1. Proposal created at height 1000.
2. Voting period is 100 blocks.
3. At height 1050 (which should be valid for voting):

   * Expiration height = `1000 + 100 = 1100`.
   * Current comparison: `(1100 >= 1050)` = true.
   * This incorrectly marks the proposal as expired.
4. The proposal is marked as expired 50 blocks too early.

This can be verified by attaching the test below to [`precompile/modules/minitia_stdlib/sources/multisig.move`](https://github.com/initia-labs/movevm/blob/7096b76ba9705d4d932808e9c80b72101eafc0a8/precompile/modules/minitia_stdlib/sources/multisig.move# L1-L1012) and run test with `initiad move test --path ./precompile/modules/minitia_stdlib --filter test_proposal_expiration_bug --statistics`.
```

#[test(
    account1 = @0x101, account2 = @0x102, account3 = @0x103
)]
#[expected_failure(abort_code = 0x30005)] // Add this line to expect the EPROPOSAL_EXPIRED error
fun test_proposal_expiration_bug(
    account1: signer,
    account2: signer,
    account3: signer
) acquires MultisigWallet {
    // create multisig wallet
    let addr1 = signer::address_of(&account1);
    let addr2 = signer::address_of(&account2);
    let addr3 = signer::address_of(&account3);

    // Create multisig with 100 block voting period
    create_multisig_account(
        &account1,
        string::utf8(b"multisig wallet"),
        vector[addr1, addr2, addr3],
        2,
        option::some(100), // max voting period of 100 blocks
        option::none()
    );
    let multisig_addr = object::create_object_address(&addr1, b"multisig wallet");

    // Set initial block height to 1000
    set_block_info(1000, 0);

    // Create proposal at height 1000
    create_proposal(
        &account1,
        multisig_addr,
        @minitia_std,
        string::utf8(b"multisig"),
        string::utf8(b"update_config"),
        vector[],
        vector[
            std::bcs::to_bytes(&vector[addr1, addr2]),
            std::bcs::to_bytes(&2u64),
            std::bcs::to_bytes(&option::none<u64>()),
            std::bcs::to_bytes(&option::none<u64>())
        ]
    );

    // Move to height 1101 (proposal should be expired)
    // Since 1101 > (1000 + 100)
    set_block_info(1101, 0);

    // This should fail with EPROPOSAL_EXPIRED
    vote_proposal(&account1, multisig_addr, 1, true);
}
```

**Console logs:**
```

INCLUDING DEPENDENCY MoveNursery
INCLUDING DEPENDENCY MoveStdlib
BUILDING MinitiaStdlib
Running Move unit tests
[ FAIL    ] 0x1::multisig::test_proposal_expiration_bug

Test Statistics:

┌─────────────────────────────────────────────┬────────────┬───────────────────────────┐
│                  Test Name                  │    Time    │         Gas Used          │
├─────────────────────────────────────────────┼────────────┼───────────────────────────┤
│ 0x1::multisig::test_proposal_expiration_bug │   0.047    │           61103           │
└─────────────────────────────────────────────┴────────────┴───────────────────────────┘

Test failures:

Failures in 0x1::multisig:

┌── test_proposal_expiration_bug ──────
│ Test did not error as expected
└──────────────────

Test result: FAILED. Total tests: 1; passed: 0; failed: 1
```

After implementing the fix below:
```

INCLUDING DEPENDENCY MoveNursery
INCLUDING DEPENDENCY MoveStdlib
BUILDING MinitiaStdlib
Running Move unit tests
[ PASS    ] 0x1::multisig::test_proposal_expiration_bug

Test Statistics:

┌─────────────────────────────────────────────┬────────────┬───────────────────────────┐
│                  Test Name                  │    Time    │         Gas Used          │
├─────────────────────────────────────────────┼────────────┼───────────────────────────┤
│ 0x1::multisig::test_proposal_expiration_bug │   0.054    │           59229           │
└─────────────────────────────────────────────┴────────────┴───────────────────────────┘

Test result: OK. Total tests: 1; passed: 1; failed: 0
```

### Recommended mitigation steps

Fix the comparison logic in `is_proposal_expired`:
```

fun is_proposal_expired(
    max_period: &Period, proposal_height: u64, proposal_timestamp: u64
): bool {
    let (height, timestamp) = get_block_info();
    let expired_height =
        if (option::is_some(&max_period.height)) {
            let max_voting_period_height = *option::borrow(&max_period.height);
-            (max_voting_period_height + proposal_height) >= height
+            height >= (max_voting_period_height + proposal_height)
        } else { false };

    let expired_timestamp =
        if (option::is_some(&max_period.timestamp)) {
            let max_voting_period_timestamp = *option::borrow(&max_period.timestamp);
-            (max_voting_period_timestamp + proposal_timestamp) >= timestamp
+            timestamp >= (max_voting_period_timestamp + proposal_timestamp)
        } else { false };

    expired_height || expired_timestamp
}
```

**[beer-1 (Initia) confirmed, but disagreed with severity and commented](https://code4rena.com/audits/2025-01-initia-move/submissions/F-41?commentParent=jg63sWbRfHS):**

> Good finding; but want to lower the severity to low. Expiration does not affect a lot on the logics and only affect to specific multis account.

**[LSDan (judge) decreased severity to Medium and commented](https://code4rena.com/audits/2025-01-initia-move/submissions/F-41?commentParent=jg63sWbRfHS&commentChild=zrGBCucVwuf):**

> I disagree. This is guaranteed to happen and does impact the desired functionality of the protocol. As such, it fits as a medium. Please let me know if I’m misunderstanding anything.

---
