# M-1: User is able to DOS and grief node by repeatedly calling deposit

## Summary
The bug report discusses an issue found in the SUI network's deposit function. The function does not perform a minimum deposit amount/fee check, allowing users to submit empty or negligible-value deposits. This can potentially cause a denial of service (DOS) attack and drain the node's funds. The report includes a proof of concept (PoC) and suggests implementing a check to prevent this issue.

## Details
Source: https://github.com/sherlock-audit/2025-04-zetachain-cross-chain-judging/issues/45 

## Found by 
BrothersInCode, g

### Summary

### Description

Any user can initiate a deposit on the SUI network by calling the `gateway.move::deposit` function:

```javascript
public entry fun deposit<T>(
    gateway: &mut Gateway,
    coin: Coin<T>,
    receiver: String,
    ctx: &mut TxContext,
) {
    let amount = coin.value();
    let coin_name = coin_name<T>();

    check_receiver_and_deposit_to_vault(gateway, coin, receiver);

    // Emit deposit event
    event::emit(DepositEvent {
        coin_type: coin_name,
        amount: amount,
        sender: tx_context::sender(ctx),
        receiver: receiver,
    });
}
```

This function in turn calls`check_receiver_and_deposit_to_vault`:

```javascript
// Validates receiver and deposits the coin into the vault
fun check_receiver_and_deposit_to_vault<T>(gateway: &mut Gateway, coin: Coin<T>, receiver: String) {
    assert!(receiver.length() == ReceiverAddressLength, EInvalidReceiverAddress);
    assert!(is_whitelisted<T>(gateway), ENotWhitelisted);
    assert!(!gateway.deposit_paused, EDepositPaused);

    // Perform the deposit
    let coin_name = coin_name<T>();
    let vault = bag::borrow_mut<String, Vault<T>>(&mut gateway.vaults, coin_name);
    balance::join(&mut vault.balance, coin.into_balance());
}
```

This helper function performs a series of checks, including:

- Verifying the receiver address format
- Checking that the coin type is whitelisted
- Ensuring that deposits are not currently paused

However, SUI's deposit flow does not perform a minimum deposit amount/fee check.

Note that in all of the other implementations (EVM, Solana, TON) these checks are correctly performed.  


### Root Cause

The root cause of this issue is that there is no minimum amount/fee check enforced inside the following [function](https://github.com/sherlock-audit/2025-04-zetachain-cross-chain/blob/e3caacef006bcef90af3035ed79d95075e31264d/protocol-contracts-sui/sources/gateway.move#L181-L199)

### Internal Pre-conditions

None

### External Pre-conditions

None

### Attack Path

1. Any user can call `gateway.move::deposit` with `coin` amount set to 0 or a neglible amount
2. User repeats this as many times as he wants



### Impact

There are several impacts here:

- Without a minimum amount or fee requirement, users can submit empty or negligible-value deposits, which will overwhelm the node’s processing logic which in turn will drain the nodes’ funds and cause a potential DOS.
- SUI's nodes have a maximum event processing limit of 50. A malicious user could exploit this by spamming deposit transactions, quickly hitting the limit with their own events and easily causing DOS for other users

### PoC
- Paste the following test-function inside `gateway_tests.move`:
```javascript
#[test]
fun test_deposit_zero_amount() {
    let mut scenario = ts::begin(@0xA);
    setup(&mut scenario);

    ts::next_tx(&mut scenario, @0xA);
    {
        let mut gateway = scenario.take_shared<Gateway>();
        let admin_cap = ts::take_from_address<AdminCap>(&scenario, @0xA);

        let coin = coin::mint_for_testing<SUI>(0, scenario.ctx()); // deposit 0 amount
        let eth_addr = ValidEthAddr.to_string().to_ascii();

        deposit(&mut gateway, coin, eth_addr, scenario.ctx());

        ts::return_to_address(@0xA, admin_cap);
        ts::return_shared(gateway);
    };
    ts::end(scenario);
}
```
- run `sui move test test_deposit_zero_amount`
- Logs:
```diff
Running Move unit tests
[ PASS    ] gateway::gateway_tests::test_deposit_zero_amount
Test result: OK. Total tests: 1; passed: 1; failed: 0
```

### Mitigation

Ensure that users are not able to perform empty deposit calls.
